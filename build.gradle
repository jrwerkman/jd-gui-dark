buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:5.3.0'    // RPM & DEB support
        classpath 'edu.sc.seis.launch4j:launch4j:3.0.6'
        classpath 'net.sf.proguard:proguard-gradle:6.1.0'
        classpath 'com.github.johnrengelman:shadow:5.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.github.johnrengelman.shadow'

// Common configuration //
rootProject.version='1.6.6'
rootProject.ext.set('jdCoreVersion', '1.1.3')

// Prefer Gradle Java toolchain for reproducible builds. Target Java 21 (latest LTS).
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Fallback compatibility flags for older Gradle plugin consumers
targetCompatibility = '21'

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    tasks.withType(JavaCompile) {
        // Use toolchain where supported; keep compatibility properties in sync for older Gradle versions
        sourceCompatibility = targetCompatibility = '21'
        options.compilerArgs << '-Xlint:deprecation'
        options.compilerArgs << '-Xlint:unchecked'
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }

    configurations {
        provided
        compile.extendsFrom provided
    }
}

// 'cleanIdea' task extension //
cleanIdea.doFirst {
    delete project.name + '.iws'
    delete 'out'
    followSymlinks = true
}

// All in one JAR file //
subprojects.each { subproject ->
    evaluationDependsOn(subproject.path)
}

shadowJar {
    archiveBaseName = project.name
    archiveVersion = project.version
    archiveClassifier = ''
    
    // Include app module (main classes and FlatLaf)
    from project(':app').sourceSets.main.output
    from project(':services').sourceSets.main.output  
    from project(':api').sourceSets.main.output
    
    // Include all runtime dependencies from app module
    configurations = [project(':app').configurations.runtimeClasspath]
    
    manifest {
        attributes 'Main-Class': 'org.jd.gui.App'
    }
    
    // Exclude duplicate files
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA' 
    exclude 'META-INF/*.RSA'
}

// Minify JAR file //
task proguard(type: proguard.gradle.ProGuardTask, dependsOn: 'shadowJar') {
    configuration 'src/proguard/resources/proguard.config.txt'
    injars jar.archivePath
    outjars 'build/libs/' + project.name + '-' + project.version + '-min.jar'
    // JDK 9+ removed rt.jar; avoid hard-coding JDK internals. If running under a JDK with jmods, include that,
    // otherwise rely on proguard's default library handling or provide explicit library jars in config.
    def javaHome = System.getProperty('java.home')
    def jmodsDir = file(javaHome + '/jmods')
    if (jmodsDir.exists()) {
        libraryjars jmodsDir
    } else {
        // older JDKs may have rt.jar
        def rt = file(javaHome + '/lib/rt.jar')
        if (rt.exists()) {
            libraryjars rt
        }
    }
}

// Java executable wrapper for Windows //
launch4j {
    createExe.dependsOn 'shadowJar'

    version = textVersion = project.version
    productName = 'JD-GUI'
    description = 'JD-GUI Windows Wrapper'
    fileDescription = 'JD-GUI (C) 2008-2019 Emmanuel Dupuy'
    icon = "${projectDir}/src/launch4j/resources/images/jd-gui.ico"
    //jar "${projectDir}/${proguard.outJarFiles[0]}"
    mainClassName = 'org.jd.gui.App'
    bundledJrePath = '%JAVA_HOME%'
}

// Packages for Linux //
ospackage {
    buildDeb.dependsOn 'proguard'
    buildRpm.dependsOn 'proguard'

    license = file('LICENSE')
    maintainer 'Emmanuel Dupuy <emmanue1@users.noreply.github.com>'
    os LINUX
    packageDescription 'JD-GUI, a standalone graphical utility that displays Java sources from CLASS files'
    packageGroup 'java'
    packageName project.name
    release '0'
    summary 'A Java Decompiler'
    url 'https://github.com/java-decompiler/jd-gui'

    into '/opt/' + project.name
    from (proguard.outJarFiles[0]) {
        fileMode 0755
    }
    from ('src/linux/resources/') {
        fileMode 0755
    }
    from 'LICENSE', 'NOTICE', 'README.md'

    postInstall 'cd /opt/' + project.name + '; ln -s ./' + file(proguard.outJarFiles[0]).name + ' ./jd-gui.jar; xdg-icon-resource install --size 128 --novendor ./jd_icon_128.png jd-gui; xdg-desktop-menu install ./*.desktop'
    preUninstall 'cd /opt/' + project.name + '; rm -f ./jd-gui.jar; rm -fr ./ext; xdg-desktop-menu uninstall ./*.desktop'
}

// Distributions for OSX and Windows //
distributions {
    osx.contents {
        into('JD-GUI.app/Contents') {
            from('src/osx/resources') {
                include 'Info.plist'
                expand VERSION: project.version,
                       JAR: file(proguard.outJarFiles[0]).name
            }
        }
        into('JD-GUI.app/Contents/MacOS') {
            from('src/osx/resources') {
                include 'universalJavaApplicationStub.sh'
                fileMode 0755
            }
        }
        into('JD-GUI.app/Contents/Resources/Java') {
            from proguard.outJarFiles[0]
        }
        from 'LICENSE', 'NOTICE', 'README.md'
    }
    windows.contents {
        from 'build/launch4j/jd-gui.exe'
        from 'LICENSE', 'NOTICE', 'README.md'
    }

    installWindowsDist.dependsOn createExe
    windowsDistTar.dependsOn createExe
    windowsDistZip.dependsOn createExe

    installOsxDist.dependsOn 'proguard'
    osxDistTar.dependsOn 'proguard'
    osxDistZip.dependsOn 'proguard'
}

build.finalizedBy buildDeb
build.finalizedBy buildRpm
